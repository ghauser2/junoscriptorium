version 1.0;

ns junos = "http://xml.juniper.net/junos/*/junos";
ns xnm = "http://xml.juniper.net/xnm/1.1/xnm";
ns jcs = "http://xml.juniper.net/junos/commit-scripts/1.0";

/*
 * This event script mimicks Cisco's absolut vty timeout functionality.
 * It DOES NOT work on 8.5 but works on 9.2. It uses jade_auth_success as
 * the generated event to cause the script to be run every 60 seconds,
 * this can be changed to fit your environment.
 *
 * Ths event-options to trigger this script are below:
 *
 * {master}
 * user@cli> show configuration event-options generate-event {
 *     jade_auth_success time-interval 60; } policy logout {
 *     events jade_auth_success;
 *     then {
 *         event-script logout.slax;
 *     }
 * }
 * event-script {
 *     file logout.slax;
 * }
 */

import "../import/junos.xsl";

var $cliTimeout = 300;

match / {
    <op-script-results> {
        var $uptimeInfo = jcs:invoke ('get-system-uptime-information');
        var $uptimeSeconds = $uptimeInfo/current-time/date-time/@junos:seconds;
        var $userInfo = jcs:invoke ('get-system-users-information');
        for-each ($userInfo/uptime-information/user-table/user-entry) {
            var $diff = $uptimeSeconds - login-time/@junos:seconds;
            if ($diff > $cliTimeout) {
                var $runcmd = <command> "request system logout terminal " _ tty;
                var $outcmd = jcs:invoke ($runcmd);
            }
        }
    }
}

